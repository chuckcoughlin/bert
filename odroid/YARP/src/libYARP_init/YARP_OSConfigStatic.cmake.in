# Copyright (C) 2006-2018 Istituto Italiano di Tecnologia (IIT)
# All rights reserved.
#
# This software may be modified and distributed under the terms of the
# BSD-3-Clause license. See the accompanying LICENSE file for details.


# For static builds, all the dependencies used by targets in YARP_OSTargets.cmake
# must be available before including the file, but due to the recursive
# dependencies in YARP_init, we are forced to use a hack


set(YARP_OS_VERSION @PACKAGE_VERSION@)

@PACKAGE_INIT@

function(_YARP_INCLUDE_TARGETS_FILE _component)
  set(CMAKE_FIND_PACKAGE_NAME ${_component}_static_hack)

  if(EXISTS "${YARP_CMAKECONFIG_DIR}/${_component}/${_component}Targets.cmake")
    include("${YARP_CMAKECONFIG_DIR}/${_component}/${_component}Targets.cmake")
    if(NOT "${_component}" STREQUAL "YARP_OS")
      list(APPEND _yarp_included_components ${_component})
    endif()

    # Extract missing targets from the error message (WARNING, not really safe
    # towards new CMake versions).
    string(REGEX REPLACE ".+:  (.+)" "\\1" _${_component}_targets "${${_component}_static_hack_NOT_FOUND_MESSAGE}")
    string(REGEX REPLACE " " ";" _${_component}_targets "${_${_component}_targets}")
    list(APPEND _yarp_expected_targets ${_${_component}_targets})
    string(REGEX REPLACE "YARP::" "" _${_component}_targets "${_${_component}_targets}")

    foreach(_next_target ${_${_component}_targets})
      if(NOT "${_next_target}" MATCHES "YARP_")
        set(_next_component "YARP_${_next_target}")
      else()
        set(_next_component "${_next_target}")
      endif()
      if(NOT TARGET YARP::${_next_target})
        _yarp_include_targets_file(${_next_component})
      endif()
    endforeach()
  endif()

  if(NOT "${_yarp_included_components}" STREQUAL "")
    list(REMOVE_DUPLICATES _yarp_included_components)
  endif()
  set(_yarp_included_components ${_yarp_included_components} PARENT_SCOPE)

  if(NOT "${_yarp_expected_targets}" STREQUAL "")
    list(REMOVE_DUPLICATES _yarp_expected_targets)
  endif()
  set(_yarp_expected_targets ${_yarp_expected_targets} PARENT_SCOPE)
endfunction()


@PACKAGE_DEPENDENCIES@



unset(_yarp_included_components)
unset(_yarp_expected_targets)

_yarp_include_targets_file(YARP_OS)

# Properly find the dependencies. This will force to include all the
# dependencies also for packages in other exports
foreach(_component ${_yarp_included_components})
  # FIXME Remove this check when CMake 3.9 or later is required
  if (CMAKE_VERSION VERSION_LESS 3.9)
    set(_YARP_OS_FIND_PARTS_REQUIRED)
    if (YARP_OS_FIND_REQUIRED)
      set(_YARP_OS_FIND_PARTS_REQUIRED REQUIRED)
    endif()
    set(_YARP_OS_FIND_PARTS_QUIET)
    if (YARP_OS_FIND_QUIETLY)
      set(_YARP_OS_FIND_PARTS_QUIET QUIET)
    endif()
    find_package(${_component}
                 ${_YARP_OS_FIND_PARTS_QUIET}
                 ${_YARP_OS_FIND_PARTS_REQUIRED}
                 HINTS "${YARP_CMAKECONFIG_DIR}"
                 NO_DEFAULT_PATH)
  else()
    find_dependency(${_component}
                    HINTS "${YARP_CMAKECONFIG_DIR}"
                    NO_DEFAULT_PATH)
  endif()
endforeach()


# Finally perform the check, usually performed inside the target files
foreach(_target ${_yarp_expected_targets})
  if(NOT TARGET "${_target}" )
    message(FATAL_ERROR "set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE_targets \"${${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE_targets} ${_target}\")")
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE_targets "${${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE_targets} ${_target}")
  endif()
endforeach()

if(DEFINED ${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE_targets)
  if(CMAKE_FIND_PACKAGE_NAME)
    set( ${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    set( ${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "The following imported targets are referenced, but are missing: ${${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE_targets}")
  else()
    message(FATAL_ERROR "The following imported targets are referenced, but are missing: ${${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE_targets}")
  endif()
endif()
unset(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE_targets)


@INCLUDED_FILE_CONTENT@
